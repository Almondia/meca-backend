configurations {
    asciidoctorExtensions
}

// 변수 선언
ext {
    snippetsDir = file('build/generated-snippets') // rest-docs 스니펫 위치 설정
}

dependencies {
    // asciidoc
    // 1.build/generated-snippets 에 생긴 .adoc 조각들을 프로젝트 내의 .adoc 파일에서 읽어들일 수 있도록 연동
    // 2. .adoc 파일을 HTML로 만들어 export 해줌
    asciidoctorExtensions 'org.springframework.restdocs:spring-restdocs-asciidoctor:2.0.6.RELEASE'

    // restdocs-mockmvc의 testCompile 구성 -> mockMvc를 사용해서 snippets 조각들을 뽑아냄
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc:2.0.6.RELEASE'
}


asciidoctor {
    inputs.dir snippetsDir
    configurations 'asciidoctorExtensions'
    dependsOn test
    sources {
        include("**/index.adoc", "**/common/*.adoc")
    }
    baseDirFollowsSourceDir()
}

asciidoctor.doFirst {
    delete file('src/main/resources/static/docs')
}

// 커스텀 task 선언: asciidoctor 동작에 의존하고 해당 동작 이후 생성된 파일을 복사하는 task
task copyDocument(type: Copy) {
    dependsOn asciidoctor
    from file("build/docs/asciidoc")
    into file("src/main/resources/static/docs")
}

build {
    dependsOn copyDocument
}